# Maintainer: ArchAssault <team@archassault.org>
pkgname=w3af
pkgver=1.6.0.3
pkgrel=1
groups=('archassault' 'archassault-webapp' 'archassault-fuzzer' 'archassault-scanners')
pkgdesc="Web Application Attack and Audit Framework"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='http://w3af.sourceforge.net/'
license=('GPL')
if [[ $CARCH == x86_64 ]]; then
     depends=('python2' 'python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'python2-pdfminer' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'sqlite' 'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3'
         'python2-xdot' 'libxml2' 'libxslt' 'lib32-glibc' 'halberd')
else
     depends=('python2' 'python2-clamd' 'python2-pybloomfiltermmap' 'python2-esmre'
         'python2-phply' 'python2-pysqlite' 'python2-nltk' 'python2-chardet'
         'python2-pdfminer' 'python2-futures' 'python2-pyopenssl' 'python2-lxml'
         'scapy' 'python2-guess-language' 'python2-cluster' 'python2-msgpack'
         'sqlite' 'python2-ntlm' 'python2-pygithub' 'python2-gitpython'
         'graphviz' 'pygtksourceview2' 'pygtk' 'gtk2' 'python2-beautifulsoup3'
         'python2-xdot' 'libxml2' 'libxslt' 'halberd')
fi
options=('!strip')
source=('git+https://github.com/andresriancho/w3af'
        'w3af.desktop')
sha512sums=('SKIP'
            'b1c643340f3afbe5e34f6d9afd5d93c8bd336afa6093b94e0120aa4f4b3ffff5c87996b5d40a0c4937db263dc2e6020a3e9ff2a03f6850a25d4f9aa876e4d8bf')

_checkout() {
  cd "$srcdir/w3af"
  if [[ $(git describe --tags) != $pkgver ]] ; then
    git checkout $pkgver
  fi
}

prepare() {
  cd "$srcdir/w3af"

  _checkout

  # Delete some of the stuff from w3af-extlib (stuff that needs to be installed) -> dependencies
  rm -rf w3af/extlib/fpconst-0.7.2
  rm -rf w3af/extlib/SOAPpy

  # Remove dependency checks.
  sed -i 's/dependency_check()/#&/' w3af_{console,gui}

  # Change the default metasploit location.
  #sed -i 's|/opt/metasploit3|/usr/share/metasploit|' core/controllers/miscSettings.py
}

package() {
  cd "$srcdir/w3af"

  _checkout

  # Base directories.
  install -dm755 "$pkgdir/usr/share/w3af"
  install -dm755 "$pkgdir/usr/bin"
  install -dm755 "$pkgdir/usr/share/applications/"

  cp --no-preserve=ownership -a * "$pkgdir/usr/share/w3af"

  install -Dm644 "$srcdir/w3af.desktop" "$pkgdir/usr/share/applications"
  find "${pkgdir}" -type f | xargs sed -i 's|/usr/bin/env python|/usr/bin/env python2|'
  find "${pkgdir}" -type f -name '*.py' | xargs sed -i 's|/usr/bin/python|/usr/bin/python2|'
  find "${pkgdir}"/usr/share/w3af/ -type f | xargs sed -i 's|/usr/bin/python|/usr/bin/python2|'
  # fixing one python file
  sed -i 's|python222.5|python2|' $pkgdir/usr/share/w3af/w3af/core/data/parsers/pynarcissus/jsparser.py
  # Joint script for w3af_console.
  cat > "$pkgdir/usr/bin/w3af" <<EOF
#!/bin/sh
exec python2 /usr/share/w3af/w3af_console "\$@"
EOF

  # Joint script for w3af_gui.
  cat > "$pkgdir/usr/bin/w3af-gui" <<EOF
#!/bin/sh
exec python2 /usr/share/w3af/w3af_gui "\$@"
EOF

  chmod +x "$pkgdir/usr/share/w3af"/w3af_{console,gui} "$pkgdir/usr/bin"/{w3af,w3af-gui}
}
