# Maintainer: ArchAssault <team archassault.org>
pkgname=aiengine
pkgver=0.7
pkgrel=1
groups=('archassault' 'archassault-networking')
pkgdesc="A packet inspection engine with capabilities of learning without any human intervention."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="https://bitbucket.org/camp0/aiengine"
license=('GPL3')
if [ $CARCH == 'x86_64' ]; then
     makedepends=('boost-libs' 'python2' 'gcc-libs-multilib')
else 
     makedepends=('boost-libs' 'python2' 'gcc-libs')
fi
depends=('pcre' 'libpcap' 'boost-libs' 'python2')
source=("https://bitbucket.org/camp0/aiengine/downloads/aiengine-$pkgver.tar.gz")
sha512sums=('0d041bb024abf35b214ac6ba042b403aa53a75b1ecd84ad99297b97408893b75f6a405b186274ece225eb5ece25c1ca71dba709911b79056f9f451129a2b7b86')

prepare(){
   grep -iRl 'python' "$srcdir/aiengine-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}  

build() {
  cd $srcdir/aiengine-$pkgver
  ./autogen.sh
  export PYTHON=/usr/bin/python2
  ./configure --prefix=/usr
  make
}

package() {
  cd $srcdir/aiengine-$pkgver
  make DESTDIR=$pkgdir install
}
