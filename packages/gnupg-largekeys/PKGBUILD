# Maintainer: ArchAssault <team@archassault.org>
# Contributor: Ido Rosen <ido@kernel.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>
# Contributor: Andreas Radke <andyrtr@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>
pkgname=gnupg-largekeys
pkgver=2.0.25
pkgrel=1
pkgdesc='Complete and free implementation of the OpenPGP standard'
url='http://www.gnupg.org/'
license=('GPL')
arch=('i686' 'x86_64' 'armv7h' 'armv6h')
optdepends=('curl: gpg2keys_curl'
            'libldap: gpg2keys_ldap'
            'libusb-compat: scdaemon')
makedepends=('curl' 'libldap' 'libusb-compat')
depends=('bzip2' 'libksba' 'libgcrypt' 'pth' 'libassuan' 'readline' 'pinentry' 'dirmngr')
source=("ftp://ftp.gnupg.org/gcrypt/${pkgname%%-largekeys}/${pkgname%%-largekeys}-${pkgver}.tar.bz2"
        'protect-tool-env.patch'
        'gnupg2-large-keys.patch'
        'filter.patch'
        'install')
sha512sums=('8b80c86ec53ed3b82d15a8e316baf175691e06e1bcac28d66c61fd37d07e26196a7ee7855e6eb958c301cee764866cc3cf00e5ec0aef23abddf7427bb3d23fb1'
            '903568aa053c5f4eb3f334071ac0dbc085045a960e9312da7e55e4a80e5c6fa6f3ad0b0ea3b464fc05d17193dac46ef4ab6e20dca7e57dc93ed213b11cb80bbc'
            'a03c91fb20f2ec900bc5d91d0fb0f2be4dc36df8f9550cc41894736979a07e8321895a9eca872d2820a44cebb2bd2c12aa27de470c10b7f75010f528b1130769'
            'fb73bad45768bbcc763b08c76c616277eeaed1494611fbd41389edd2dc0edd43d504eedf32806a27a2355bfe40763737be45255862f62addafda9d70de4fea55'
            'b3a125ee70b2042d158fc68e9e15660a87c5c30e5810570ea40376f2df3701973cf1c9e6481953b0415d9368ed7e93d6540274814207eee7e9a6852c51883ba3')

install=install

conflicts=('gnupg2' 'gnupg')
provides=("gnupg2=${pkgver}" "gnupg=${pkgver}")
replaces=('gnupg2' 'gnupg')

prepare() {
	cd "${srcdir}/${pkgname%%-largekeys}-${pkgver}"
	patch -p1 -i ../protect-tool-env.patch # FS#31900
	patch -p1 -i ../filter.patch
	patch -p1 -i ../gnupg2-large-keys.patch
}

build() {
	cd "${srcdir}/${pkgname%%-largekeys}-${pkgver}"
	./configure \
		--prefix=/usr \
		--sysconfdir=/etc \
		--sbindir=/usr/bin \
		--libexecdir=/usr/lib/gnupg \
		--enable-maintainer-mode \
		--enable-standard-socket \
		--enable-symcryptrun \
		--enable-gpgtar \

	make
}

check() {
	cd "${srcdir}/gnupg-${pkgver}"
	make check
}

package() {
	cd "${srcdir}/gnupg-${pkgver}"
	make DESTDIR="${pkgdir}" install
	ln -s gpg2 "${pkgdir}"/usr/bin/gpg
	ln -s gpgv2 "${pkgdir}"/usr/bin/gpgv
	ln -s gpg2.1.gz "${pkgdir}"/usr/share/man/man1/gpg.1.gz
	rm "${pkgdir}/usr/share/gnupg/com-certs.pem" # FS#33059
}
