# Maintainer: ArchAssault <team@archassault.org>
# Contributor drathir <drathir87@gmail.com>
pkgname=perl-class-load-xs
_lastauthor=E/ET/ETHER
_pkgname=Class-Load-XS
pkgver=0.08
pkgrel=2
pkgdesc="XS implementation of parts of Class::Load"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
license=('PerlArtistic2')
options=('!emptydirs')
#perl-class-load-xs W: Dependency included and not needed ('perl-class-load')
depends=('perl-class-load')
checkdepends=('perl-test-requires' 'perl-test-without-module')
url="http://search.cpan.org/dist/${_pkgname}/"
source=(http://search.cpan.org/CPAN/authors/id/${_lastauthor}/${_pkgname}-${pkgver}.tar.gz)
sha256sums=('67f5e120ed79d853ad5941bca42d5fe4a1ed2324cc8f79d2918aead9d8cf1be9')

build() {
  export PERL_MM_USE_DEFAULT=1 PERL_AUTOINSTALL="--skipdeps" \
    PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='$pkgdir'" \
    PERL_MB_OPT="--installdirs vendor --destdir '$pkgdir'" \
    MODULEBUILDRC=/dev/null

  cd "${srcdir}/${_pkgname}-${pkgver}"
  perl Makefile.PL
  make
}
check() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make test
}
package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make install
}
