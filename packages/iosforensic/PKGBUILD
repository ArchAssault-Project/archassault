# Maintainer: ArchAssualt <team@archassault.org>
pkgname=iosforensic
pkgver=1.0
pkgrel=1
pkgdesc="iOS forensic tool https://www.owasp.org/index.php/Projects/OWASP_iOSForensic"
arch=('any')
url="https://github.com/Flo354/iOSForensic"
license=('custom')
depends=('python2' 'sshpass' 'sqlite3' 'python2-magic' 'libplist' 'openssh')
source=("https://github.com/Flo354/iOSForensic/archive/$pkgver.tar.gz")
sha512sums=('5034f5cdbd2f5facf45d67f769ade689ffa164eddb86eed7237955e2f2695f387a1af1da459dbc523b55a68d2ee62e30ab1fdb4e7ac388aee6977f724b2bad31')

prepare(){
  grep -iRl 'python' "$srcdir/iOSForensic-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}
 
package(){
  cd "$srcdir/iOSForensic-$pkgver"
  install -dm755 "$pkgdir/usr/share/$pkgname"
  install -dm755 "$pkgdir/usr/bin"
   
  install -Dm755 *.py "$pkgdir/usr/share/$pkgname/"
  install -Dm644 changelog "$pkgdir/usr/share/$pkgname/"
  install -Dm644 README.md "$pkgdir/usr/share/licenses/$pkgname/README.md"

cat > "$pkgdir/usr/bin/iosforensic" <<EOF
#!/bin/sh
cd /usr/share/iosforensic
python2 iOSForensic.py "\$@"
EOF

chmod +x $pkgdir/usr/bin/$pkgname
}

# vim:set ts=2 sw=2 et:
