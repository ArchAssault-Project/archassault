# Maintainer: ArchAssault <team@archassault.org>
pkgname=killerbee-svn
pkgrel=1
pkgver=89
groups=('archassault' 'archassault-wireless' 'archassault-exploit')
pkgdesc="Framework and tools for exploiting ZigBee and IEEE 802.15.4 networks."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url='https://code.google.com/p/killerbee/'
license=('BSD')
depends=('python2' 'python2-crypto' 'python2-pyusb' 'pygtk' 'python2-cairo' 'python2-pyserial')
conflicts=('killerbee')
provides=('killerbee')
replaces=('killerbee')
options=(!emptydirs)
source=("svn+http://killerbee.googlecode.com/svn/trunk/")
sha512sums=('SKIP')

pkgver() {
  cd "${srcdir}/trunk"
  svnversion | tr -d [A-z]
}

prepare() {
  cd "${srcdir}/trunk/killerbee"
  sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|' tools/*
}

package() {
  cd "${srcdir}/trunk/killerbee"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  install -Dm644 LICENSE.txt "$pkgdir/usr/share/licenses/$pkgname/LICENSE.txt"
  find $pkgdir -type f -iname "*.py" |xargs sed -i 's|#!/usr/bin/env python|#!/usr/bin/env python2|'
}
