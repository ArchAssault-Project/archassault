# Maintainer: ArchAssault <team@archassault.org>
pkgname=netdiscover
_pkgver=0.3
_betanum=6
pkgver=${_pkgver}.beta${_betanum}
pkgrel=2
groups=('archassault' 'archassault-reconnaissance' 'archassault-wireless')
pkgdesc="An active/passive address reconnaissancenaissance tool, mainly developed for those wireless networks without dhcp server, when you are wardriving. It can be also used on hub/switched networks."
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://nixgeneration.com/~jaime/${pkgname}/"
license=('GPL')
depends=('libnet' 'libpcap')
source=("http://nixgeneration.com/~jaime/${pkgname}/releases/${pkgname}-${_pkgver}-beta${_betanum}.tar.gz"
        'misc.diff'
        'oui.diff')
sha512sums=('b8205322c34684642d52bf58f8fffa12a0dea387da713caf2935ba348c74b2e6cac717bf1626c9de816e45dce7c42b6b9584b5253351040af806f7ba262ce4d2'
         '3b00414c351b05a87301098cdef66bd119f9a156e04fffc3ce7a1a426e0a4e60a8c8a31bab552a9762646a76d094828e3da8f7cb3ab23585f937d623b28c4d82'
         'c2fb66559a89e2bcc574ae7753f349c885b10871473d6fe9640b4ae286ffe84a0f7e688c2357c68803452ca9c5fafe1df6651c67051519ff2044f655c05e467f')

prepare() {
    cd ${pkgname}-${_pkgver}-beta${_betanum}
    patch -p0 -i ../oui.diff
    patch -p0 -i ../misc.diff
}

build() {
    cd ${pkgname}-${_pkgver}-beta${_betanum}
    ./configure --prefix=/usr/share --sysconfdir=/etc/ --sbindir=/usr/bin
    make
}

package() {
    cd ${pkgname}-${_pkgver}-beta${_betanum}
    make DESTDIR="$pkgdir" install
}
