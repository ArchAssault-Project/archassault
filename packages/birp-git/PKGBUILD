# Maintainer: ArchAssault <team@archassault.org>
pkgname=birp-git
pkgver=r55.a85c8d6
pkgrel=1
pkgdesc="A tool that will assist in the security assessment of mainframe applications served over TN3270."
arch=('any')
url="https://github.com/sensepost/birp"
license=('custom')
provides=('birp')
replaces=('birp')
conflicts=('birp')
depends=('python2' 'python2-colorama' 'python2-py3270' 'ipython2' 'x3270')
source=(git+https://github.com/sensepost/birp.git LICENSE)
sha512sums=('SKIP'
            '5c0ff9ce5529f5c2d01cf25c296ccde75f056794c263765f0f652b809433c695d9836b58c5761a74494bcbb96747119bc914163dc706a9aad700d3351d688c71')


pkgver() {
  cd "$srcdir/birp"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare(){
  grep -iRl 'python' "$srcdir/birp" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}

package(){
  cd "$srcdir/birp"
  install -dm755 $pkgdir/usr/bin
  install -dm755 $pkgdir/usr/share/birp
  install -Dm755 *.py "$pkgdir/usr/share/birp/"
  install -Dm644 Readme.md "$pkgdir/usr/share/birp/"  
  install -Dm644 $srcdir/LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"

cat > "$pkgdir/usr/bin/birp" <<EOF
#!/bin/sh
cd /usr/share/birp
python2 birp.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/birp"
}
