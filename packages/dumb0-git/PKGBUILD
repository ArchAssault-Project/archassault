# Maintainer: ArchAssault <team archassault.org>
pkgname=dumb0-git
pkgver=r19.1493e74
pkgrel=1
groups=('archassault' 'archassault-scanner')
pkgdesc="A simple tool to dump users in popular forums and CMS :)"
arch=('any')
url='https://github.com/0verl0ad/Dumb0'
license=('GPL2')
depends=('perl')
makedepends=('git')
provides=('dumb0')
replaces=('dumb0')
conflicts=('dumb0')
source=("dumb0::git+https://github.com/0verl0ad/Dumb0.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/dumb0"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$srcdir/dumb0"

  sed -i '1i\#!/usr/bin/perl' Dumb0.pl
}

package() {
  cd "$srcdir/dumb0"

  install -Dm755 Dumb0.pl "$pkgdir/usr/bin/dumb0"
  install -Dm644 README.md "$pkgdir/usr/share/dumb0/README.md"

}
