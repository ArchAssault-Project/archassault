# Maintainer: ArchAssault <team archassault org>
pkgname=centry-git
pkgver=r72.6de2868
pkgrel=1
groups=('archassault' 'archassault-misc')
pkgdesc="A Panic button for protection against cold boot attacks"
arch=('any')
url='https://github.com/0xPoly/Centry'
license=('GPL3')
depends=('python') 
optdepends=('secure-delete')
makedepends=('git')
provides=('centry')
conflicts=('centry')
replaces=('centry')
source=("centry::git+https://github.com/0xPoly/Centry.git")
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/centry"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/centry"

  # Make base directories.
  install -dm755 "$pkgdir/usr/share/centry"
  install -dm755 "$pkgdir/usr/bin"
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/centry/LICENSE
  install -Dm755 centry.py "$pkgdir/usr/share/centry/centry.py"
  install -Dm644 icon.xbm "$pkgdir/usr/share/centry/"
  install -Dm644 screenshot.png "$pkgdir/usr/share/centry/"
  install -Dm644 settings.gif "$pkgdir/usr/share/centry/"
  
cat > "$pkgdir/usr/bin/centry" << EOF
#!/bin/sh
cd /usr/share/centry
python ./centry.py "\$@"
EOF

chmod +x "$pkgdir/usr/bin/centry"

}
