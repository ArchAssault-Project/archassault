# Maintainer: ArchAssault <team archassault org>
pkgname=gsa
pkgver=5.0.1
pkgrel=1
pkgdesc="Greenbone Security Assistant (gsa) - OpenVAS web frontend"
arch=('i686' 'x86_64' 'armv6h' 'armv7h')
url="http://www.openvas.org/"
license=('GPL')
groups=('archassault')
depends=('glibc' 'libmicrohttpd' 'libxml2' 'glib2' 'libssh' 'libxslt' 'gnutls' 'openvas-libraries')
makedepends=('gcc' 'automake' 'cmake' 'doxygen' 'xmltoman')
provides=('greenbone-security-assistant')
conflicts=('greenbone-security-assistant')
replaces=('greenbone-security-assistant')
backup=(etc/openvas/gsad_log.conf)
source=("http://wald.intevation.org/frs/download.php/1675/greenbone-security-assistant-$pkgver.tar.gz")
sha512sums=('2ad816d70d3e6a9c2cdcc58136a5d9794617053b96d86af9f0dd0046371e717cb9e1c51e11a64e5ef1a37ebba9cfe15d22f4468a3a12a67333387ee67ae445fb')


build() {
  cd "$srcdir/greenbone-security-assistant-$pkgver"
  cmake -DSBINDIR=/usr/bin -DCMAKE_INSTALL_PREFIX=/usr -DSYSCONFDIR=/etc -DLOCALSTATEDIR=/var .
  make
}

package() {
  cd "$srcdir/greenbone-security-assistant-$pkgver"
  make DESTDIR="$pkgdir/" install
}
