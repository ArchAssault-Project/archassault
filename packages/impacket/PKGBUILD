# Maintainer: ArchAssault <team archassault org>
pkgname=impacket
pkgver=0.9.12
pkgrel=1
pkgdesc="Collection of Python2 classes focused on providing access to network packets"
arch=('any')
url='http://oss.coresecurity.com/projects/impacket.html'
license=('Apache')
depends=('python2' 'python2-crypto')
groups=('archassault')
optdepends=('python2-pcapy: traffic sniffing example programs')
makedepends=('sed')
source=("https://pypi.python.org/packages/source/i/impacket/impacket-$pkgver.tar.gz")
sha512sums=('1e3e1290990da5701105e038ef59b9c0adc50ae01969c7b2e9d54444dbdfc069895e1e8c1c9602dbaccee9f6b4255cb9160757836b18dfbb34ad5e0174a8f72d')

prepare(){
  grep -iRl 'python' "$srcdir/impacket-$pkgver" | xargs sed -i 's|#!.*/usr/bin/python|#!/usr/bin/python2|;s|#!.*/usr/bin/env python$|#!/usr/bin/env python2|'
}


package() {
  cd "${srcdir}/impacket-${pkgver}"
  python2 setup.py install --root="${pkgdir}/" --optimize=1
  chmod 644 $pkgdir/usr/share/doc/impacket/LICENSE
  chmod 644 $pkgdir/usr/share/doc/impacket/README
  find  $pkgdir/usr/share/doc/impacket/testcases -type f  |xargs chmod 755 
  chmod 755 $pkgdir/usr/share/doc/impacket/testcases
  chmod 755 $pkgdir/usr/share/doc/impacket/testcases/ImpactPacket
  chmod 755 $pkgdir/usr/share/doc/impacket/testcases/dot11
}
