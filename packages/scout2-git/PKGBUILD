# Maintainer: ArchAssault <team@archassault.org>
pkgname=scout2-git
pkgver=r196.7cc58b4
pkgrel=1
pkgdesc="Security auditing tool for AWS environments"
url="http://isecpartners.github.io/Scout2/"
arch=('any')
license=('GPL2')
groups=('archassault')
depends=('python2' 'python2-boto' 'python2-dateutil')
makedepends=('git')
provides=('scout2')
conflicts=('scout2')
replaces=('scout2')
source=(scout2-git::git+https://github.com/iSECPartners/Scout2.git)
sha512sums=('SKIP')

pkgver() {
  cd "$srcdir/scout2-git"
  printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir"/scout2-git
  install -dm755 "$pkgdir"/usr/bin/
  install -dm755 "$pkgdir"/usr/share/scout2-git
  install -Dm644 LICENSE "$pkgdir"/usr/share/licenses/scout2-git/LICENSE
  install -m644 README.md "$pkgdir"/usr/share/scout2-git/README.md
  install -m644 Scout2.py "$pkgdir"/usr/share/scout2-git/Scout2.py
  install -m644 report.html "$pkgdir"/usr/share/scout2-git/report.html
  cp --no-preserve=ownership -r AWSScout2 inc-bootstrap inc-handlebars inc-jquery inc-scout2 rules "$pkgdir"/usr/share/scout2-git
  cat > "$pkgdir"/usr/bin/Scout2.py <<EOF
#!/bin/sh
cd /usr/share/scout2-git
python2 Scout2.py "\$@"
EOF
  chmod +x "$pkgdir"/usr/bin/Scout2.py
}


