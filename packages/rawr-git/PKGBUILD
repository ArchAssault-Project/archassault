# Maintainer: ArchAssault <team@archassault.org>
pkgname=rawr-git
pkgver=39.aac6a99
pkgrel=1
groups=('archassault' 'archassault-webapp' 'archassault-scanners')
pkgdesc="Rapid Assessment of Web Resources. A web enumerator."
arch=('any')
url="https://bitbucket.org/al14s/rawr/wiki/Home"
license=('GPL3')
makedepends=('git')
depends=('nmap' 'python2' 'graphviz' 'python2-pygraphviz' 'python2-lxml' 'phantomjs' 'python2-pillow')
replaces=('rawr')
conflicts=('rawr')
provides=('rawr')
source=("git+https://bitbucket.org/al14s/rawr.git")
sha512sums=('SKIP')

pkgver() {
  cd rawr
  echo $(git rev-list --count HEAD).$(git rev-parse --short HEAD)
}

prepare() {
  cd "$srcdir/rawr"

  sed -i '1s/env python/env python2/' rawr.py lib/requests/certs.py lib/requests/packages/chardet/chardetect.py
}

package() {
  cd "$srcdir/rawr"

  # Base directories.
  install -dm755 "$pkgdir/usr/bin" 
  install -dm755 "$pkgdir/usr/share/rawr" 

  cp -R * "$pkgdir/usr/share/rawr"

  cat > "$pkgdir/usr/bin/rawr" <<EOF
#!/bin/sh
python2 /usr/share/rawr/rawr.py
EOF

  chmod +x "$pkgdir/usr/bin/rawr"
}
